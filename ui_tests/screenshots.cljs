(ns screenshots
  {:clj-kondo/config '{:lint-as {promesa.core/let clojure.core/let}}}
  (:require ["playwright$default" :as pw :refer [chromium]]
            [promesa.core :as p]
            [nbb.core :refer [await]]))

(defn println+flush [& args]
  (apply println args)
  (flush))

(defn goto [page url]
  (.goto page url #js{:waitUntil "networkidle"}))

(def image-layouts-notebook "https://snapshots.nextjournal.com/clerk/build/48574b57e77d29b28cc158b9e747e943d15757d6/index.html#/notebooks/viewers/image_layouts.clj")

(def rule30-notebook "https://snapshots.nextjournal.com/clerk/build/48574b57e77d29b28cc158b9e747e943d15757d6/index.html#/notebooks/rule_30.clj")

(def url image-layouts-notebook)
(def browser (await (.launch chromium #js {:headless false})))
(def page-width 1280)
(def page-height 720)
(def page (await (.newPage browser #js {:viewport #js {:width page-width :height page-height}})))

(println+flush "ðŸŒ Visiting page:" url)
(await (goto page url))

(defn screenshot []
  (println+flush "ðŸ“· Starting screenshottingâ€¦")
  (await (-> (p/let [results (.locator page ".viewer-result")
                     results-count (.count results)]
               (println+flush "ðŸ“¸ Screenshotting page with bounds" (str page-width "Ã—" page-height))
               (.screenshot page #js {:path "page.png"})
               (p/loop [i 0]
                 (if (< i results-count)
                   (p/let [res (.nth results i)
                           bounds (.boundingBox res)]
                     (if (<= 250 (.-height bounds))
                       (do
                         (println+flush "ðŸ“¸ Screenshotting result with bounds" (str (.-width bounds) "Ã—" (.-height bounds)))
                         (.screenshot res #js {:path (str "result-" (inc i) ".png")}))
                       (println+flush "ðŸ¦˜ Skipping result with bounds" (str (.-width bounds) "Ã—" (.-height bounds))))
                     (p/recur (inc i)))
                   (println+flush "âœ… Done.")))))))

(screenshot)

(.close browser)
